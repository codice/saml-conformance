apply plugin: 'application'

description = 'Script and CLI to run the tests.'

group = 'org.codice.samlconf.deployment'

applicationName = 'samlconf'

mainClassName = 'org.codice.ckt.CommandKt'

dependencies {
    compile project(':external:implementations:samlconf-ddf-impl')
    compile project(':external:implementations:samlconf-keycloak-impl')
    compile project(':library')
    compile project(':external:api')
    compile project(':ctk:common')
    compile project(':ctk:idp')
    compile project(':ctk:sp')
    compile project(':deployment:suites')

    compile 'us.jimschubert:kopper-typed:0.0.3'
}

applicationDistribution.from('src/main/resources/') {
    into 'conf'
}

// Copy implementation examples into the distribution
project(':external:implementations').getSubprojects().each { subProject ->
    applicationDistribution.into('implementations/' + subProject.name.replace("samlconf-", "").replace("-impl", "")) {
        from subProject.file('build/resources/main')
        from subProject.file('build/libs')
    }
}

// We don't want to include the implementation jars in the classpath
// since they're loaded from a specific directory.
task copyToLib(type: Copy) {
    from configurations.runtime {
        exclude group: 'org.codice.samlconf.implementations'
    }
}

applicationDefaultJvmArgs = ["-Dapp.home=SAMLCTK_APP_HOME"]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('SAMLCTK_APP_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('SAMLCTK_APP_HOME', '%~dp0..')
    }
}

build.finalizedBy(installDist)